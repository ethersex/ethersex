dnl This m4 script uses quite a few divert levels, these are essentially:
dnl   1: char array in program space
dnl   2: the alias list
dnl   4: function list trailer
dnl   5: (optional) function implementations 
dnl
dnl ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
dnl
dnl   Copyright (c) 2007,2009 by Christian Dietrich <stettberger@dokucode.de>
dnl   Copyright (c) 2007,2008 by Stefan Siegl <stesie@brokenpipe.de>
dnl  
dnl   This program is free software; you can redistribute it and/or modify
dnl   it under the terms of the GNU General Public License version 3 as
dnl   published by the Free Software Foundation.
dnl  
dnl   This program is distributed in the hope that it will be useful,
dnl   but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl   GNU General Public License for more details.
dnl  
dnl   You should have received a copy of the GNU General Public License
dnl   along with this program; if not, write to the Free Software
dnl   Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.
dnl  
dnl   For more information on the GPL, please go to:
dnl   http://www.gnu.org/copyleft/gpl.html
dnl
divert(0)dnl
/* This file has been generated by known_buddies.m4 automatically.
   Please do not modify it, edit the m4 scripts instead. */
divert(1)dnl
/* Char array definitions follow */
divert(2)dnl

/* Definition of alias pointer array follows */
static const PROGMEM char * const jabber_known_buddies[] = {
divert(-1)dnl
define(`buddy_counter', 0)

define(`USER', `dnl
divert(1)dnl
`static const char PROGMEM buddy_'buddy_counter`_name[] = "$1";'

divert(2)	`buddy_'buddy_counter`_name,'
define(`buddy_counter', incr(buddy_counter))dnl
divert(-1)')

divert(4)dnl
};
divert(-1)dnl
dnl yippie, we're done!
